pragma Singleton

import qs.modules.common
import qs.modules.common.functions
import QtQuick
import Quickshell
import Quickshell.Io

Item {
    id: root

    property list<var> sections: []

    property string currentSearch: ""
    onCurrentSearchChanged: {
        console.log("Current found search result string:", currentSearch)
    }

    function startIndexing() {
        sections = []
        pageFile.start([
            Directories.quickConfigPath,
            Directories.generalConfigPath,
            Directories.barConfigPath,
            Directories.backgroundConfigPath,
            Directories.interfaceConfigPath,
            Directories.servicesConfigPath,
            Directories.advancedConfigPath,
            Directories.systemConfigPath,
            Directories.systemNetworkConfigPath,
            Directories.systemBluetoothConfigPath,
            Directories.systemAudioConfigPath,
            Directories.systemDisplayConfigPath,
            Directories.hyprlandConfigPath,
            Directories.aboutConfigPath
        ])
    }

    Component.onCompleted: startIndexing()

    Connections {
        target: Translation
        function onLanguageCodeChanged() {
            startIndexing()
        }
    }

    FileView {
        id: pageFile
        blockLoading: true

        property var files: []
        property int currentIndex: 0

        function start(filesArray) {
            files = filesArray
            currentIndex = 0
            loadNext()
        }

        function loadNext() {
            if (currentIndex >= files.length)
                return

            path = files[currentIndex]
        }

        onLoaded: {
            root.indexQmlFile(text())

            currentIndex++

            Qt.callLater(() => loadNext())
        }
    }


    // Fetches the needed string like text, title from the qml file

    function indexQmlFile(qmlText) {
        if (!qmlText)
            return

        let sectionMatches = extractBlocks(qmlText, "ContentSection")

        for (let sectionBlock of sectionMatches) {
            let title = extractProperty(sectionBlock, "title")
            let searchStrings = []
            if (title) searchStrings.push(title)

            // Extract subtitles from subsections
            let subBlocks = extractBlocks(sectionBlock, "ContentSubsection")
            for (let sub of subBlocks) {
                let subTitle = extractProperty(sub, "title")
                if (subTitle) searchStrings.push(subTitle)
            }

            // Extract labels from interactive components
            const interactiveTypes = ["ConfigSwitch", "ConfigSpinBox", "ConfigSelectionArray", "RippleButtonWithIcon"]
            for (let type of interactiveTypes) {
                let blocks = extractBlocks(sectionBlock, type)
                for (let b of blocks) {
                    let text = extractProperty(b, "text") || extractProperty(b, "mainText")
                    if (text) searchStrings.push(text)
                }
            }

            let pageIndex = extractPageIndex(qmlText)
            registerSection({
                pageIndex: pageIndex,
                title: title || "Unknown",
                searchStrings: searchStrings
            })
        }
    }

    function extractBlocks(text, type) {
        let results = []
        let pos = 0
        while (true) {
            let start = text.indexOf(type, pos)
            if (start === -1) break
            
            let braceStart = text.indexOf("{", start)
            if (braceStart === -1) {
                pos = start + type.length
                continue
            }

            let depth = 1
            let i = braceStart + 1
            let inString = false
            let quoteChar = ""

            while (i < text.length && depth > 0) {
                let char = text[i]
                if (!inString && (char === '"' || char === "'")) {
                    inString = true
                    quoteChar = char
                } else if (inString && char === quoteChar && text[i-1] !== '\\\\') {
                    inString = false
                } else if (!inString) {
                    if (char === "{") depth++
                    else if (char === "}") depth--
                }
                i++
            }
            results.push(text.substring(braceStart + 1, i - 1))
            pos = i
        }
        return results
    }

    function extractProperty(block, prop) {
        // Match both 'prop: "val"' and 'prop: Translation.tr("val")'
        let re = new RegExp(prop + "\\s*:\\s*(?:Translation\\.tr\\(\\s*)?([\"'])(.*?)\\1", "g")
        let match = re.exec(block)
        return match ? match[2] : ""
    }

    // Helper function for indexQmlFile(), extracts the page index
    
    function extractPageIndex(qmlText) {
        let m = qmlText.match(/readonly\s+property\s+int\s+index\s*:\s*(\d+)/)
        return m ? parseInt(m[1]) : -1
    }

    function tokenize(text) {
        if (!text || typeof text !== "string")
            return []

        return text
            .toLowerCase()
            .replace(/[^a-z0-9\sğüşöçıİ_\-\.]/g, " ")
            .split(/[\s_\-\.]+/)
            .filter(function(t) { return t.length > 1 })
    }

    function fuzzyMatch(word, query) {
        let wi = 0
        let qi = 0
        let score = 0

        word = word.toLowerCase()
        query = query.toLowerCase()

        while (wi < word.length && qi < query.length) {
            if (word[wi] === query[qi]) {
                score += 10
                qi++
            }
            wi++
        }

        if (qi === query.length)
            return score

        return 0
    }

    function registerSection(data) {
        const titleKey = data.title
        const searchStringsKeys = [...data.searchStrings]

        // Apply translations
        data.title = Translation.tr(titleKey)
        data.searchStrings = searchStringsKeys.map(s => Translation.tr(s))

        let combined = (titleKey + " " + searchStringsKeys.join(" ") + " " + data.title + " " + data.searchStrings.join(" ")).toLowerCase()
        
        data._tokens = tokenize(combined)
        data._searchText = combined
        
        sections.push(data)
        
        // console.log("[SearchRegistry] Registered section:", data.title, "with strings:", data.searchStrings)
    }

    function getBestResult(text) {
        let results = getSearchResult(text)
        if (results.length === 0)
            return null

        results.sort((a, b) => b.score - a.score)
        return results[0]
    }

    function getResultsRanked(text) {
        let results = getSearchResult(text)
        results.sort((a, b) => b.score - a.score)
        return results
    }

    function getSearchResult(query) {
        if (!query || query.trim() === "") return []

        query = query.toLowerCase().trim()
        let queryTokens = tokenize(query)
        let results = []

        for (let section of sections) {
            let totalScore = 0
            let bestMatch = "" 
            let bestMatchScore = 0
            let bestMatchSource = "" 
            
            // direct match in title
            if (section.title.toLowerCase().includes(query)) {
                totalScore += 1000
                if (bestMatchScore < 1000) {
                    bestMatch = section.title
                    bestMatchSource = section.title
                    bestMatchScore = 1000
                }
            }
            
            // direct match in searchStrings
            for (let searchStr of section.searchStrings) {
                let lowerStr = searchStr.toLowerCase()
                if (lowerStr.includes(query)) {
                    let score = lowerStr === query ? 800 : 500
                    totalScore += score
                    if (score > bestMatchScore) {
                        bestMatch = searchStr
                        bestMatchSource = searchStr
                        bestMatchScore = score
                    }
                }
            }
            
            for (let searchStr of section.searchStrings) {
                let searchStrLower = searchStr.toLowerCase()
                let searchTokens = tokenize(searchStrLower)
                let matchedTokenCount = 0
                let tokenScore = 0
                
                for (let qToken of queryTokens) {
                    for (let sToken of searchTokens) {
                        let score = 0
                        if (sToken.startsWith(qToken)) {
                            score = 200
                            matchedTokenCount++
                        } else if (sToken.includes(qToken)) {
                            score = 100
                            matchedTokenCount++
                        } else {
                            let fuzzyScore = fuzzyMatch(sToken, qToken)
                            if (fuzzyScore > 0) {
                                score = fuzzyScore
                                matchedTokenCount++
                            }
                        }
                        
                        if (score > 0) {
                            tokenScore += score
                        }
                    }
                }
                
                if (tokenScore > 0) {
                    totalScore += tokenScore
                    if (tokenScore > bestMatchScore && matchedTokenCount > 0) {
                        bestMatch = searchStr
                        bestMatchSource = searchStr
                        bestMatchScore = tokenScore
                    }
                }
            }
            
            if (totalScore > 0) {
                results.push({
                    pageIndex: section.pageIndex,
                    title: section.title,
                    keyword: section._searchText,
                    matchedString: bestMatch || section.title,
                    yPos: section.yPos,
                    score: totalScore
                })
            }
        }
        
        return results
    }

    function scoreResult(result, text) {
        return result.score
    }

    // Debug
    function listAllSections() {
        console.log("=== Registered Sections ===")
        for (let i = 0; i < sections.length; i++) {
            console.log(i + ":", sections[i].title, "tokens:", sections[i]._tokens)
        }
    }
}