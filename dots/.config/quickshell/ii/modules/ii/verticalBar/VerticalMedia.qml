import qs.modules.common
import qs.modules.common.widgets
import qs.services
import qs
import qs.modules.common.functions

import QtQuick
import QtQuick.Layouts
import Quickshell.Services.Mpris

import qs.modules.ii.bar as Bar

MouseArea {
    id: root
    readonly property MprisPlayer activePlayer: MprisController.activePlayer
    readonly property string cleanedTitle: StringUtils.cleanMusicTitle(activePlayer?.trackTitle) || Translation.tr("No media")

    Layout.fillHeight: true
    implicitHeight: mediaCircProg.implicitHeight + 10 // +10 for padding it looks so small if we dont add it
    implicitWidth: Appearance.sizes.verticalBarWidth

    function updateGlobalPosition() {
        if (root.visible && root.height > 0) {
            var globalPos = root.mapToItem(null, 0, 0);
            var window = root.QsWindow?.window;
            var screen = window?.screen;
            if (window && screen) {
                var absoluteX = globalPos.x;
                var absoluteY = globalPos.y;

                if (!Config.options.bar.vertical) {
                    if (Config.options.bar.bottom) {
                        absoluteY += (screen.height - window.height);
                    }
                } else {
                    if (Config.options.bar.bottom) { // Right side
                        absoluteX += (screen.width - window.width);
                    }
                }

                GlobalStates.registerMediaPosition(screen.name, absoluteX, absoluteY, root.width, root.height);
            }
        }
    }

    Timer {
        id: settlingTimer
        interval: 500
        repeat: false
        onTriggered: updateGlobalPosition()
    }

    onXChanged: settlingTimer.restart()
    onYChanged: settlingTimer.restart()
    onHeightChanged: settlingTimer.restart()
    onVisibleChanged: {
        if (visible) settlingTimer.restart()
    }

    Component.onCompleted: settlingTimer.restart()

    Timer {
        running: activePlayer?.playbackState == MprisPlaybackState.Playing
        interval: Config.options.resources.updateInterval
        repeat: true
        onTriggered: activePlayer.positionChanged()
    }

    cursorShape: Qt.PointingHandCursor
    acceptedButtons: Qt.MiddleButton | Qt.BackButton | Qt.ForwardButton | Qt.RightButton | Qt.LeftButton
    hoverEnabled: !Config.options.bar.tooltips.clickToShow
    onPressed: (event) => {
        if (event.button === Qt.MiddleButton) {
            activePlayer.togglePlaying();
        } else if (event.button === Qt.BackButton) {
            activePlayer.previous();
        } else if (event.button === Qt.ForwardButton || event.button === Qt.RightButton) {
            activePlayer.next();
        } else if (event.button === Qt.LeftButton) {
            var globalPos = root.mapToItem(null, 0, 0);
            Persistent.states.media.popupRect = Qt.rect(globalPos.x, globalPos.y, root.width, root.height);
            GlobalStates.mediaControlsOpen = !GlobalStates.mediaControlsOpen;
        }
    }

    ClippedFilledCircularProgress {
        id: mediaCircProg
        anchors.centerIn: parent
        implicitSize: 20

        lineWidth: Appearance.rounding.unsharpen
        value: activePlayer?.position / activePlayer?.length
        colPrimary: Appearance.colors.colOnSecondaryContainer
        enableAnimation: false

        Item {
            anchors.centerIn: parent
            width: mediaCircProg.implicitSize
            height: mediaCircProg.implicitSize
            
            MaterialSymbol {
                anchors.centerIn: parent
                fill: 1
                text: activePlayer?.isPlaying ? "pause" : "music_note"
                iconSize: Appearance.font.pixelSize.normal
                color: Appearance.m3colors.m3onSecondaryContainer
            }
        }
    }

    Bar.MediaPopup {
        hoverTarget: root
        active: (GlobalStates.mediaControlsOpen ? false : root.containsMouse) && (!Config.options.island.enable || activePlayer !== null)
    }
}
