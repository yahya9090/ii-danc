pragma ComponentBehavior: Bound
import QtQuick
import Quickshell.Widgets
import Quickshell.Services.Mpris
import qs.modules.common
import qs.modules.common.widgets
import qs.services

ClippingRectangle {
    id: root

    property MprisPlayer player: MprisController.activePlayer
    readonly property string artUrl: (player?.trackArtUrl ?? "").toString()
    // Simple art source resolution
    readonly property string resolvedArt: artUrl.startsWith("file://") || artUrl.startsWith("/") ? artUrl : ""

    radius: width / 2
    color: Appearance.m3colors.m3surfaceContainerHighest
    antialiasing: true

    property string _aSrc: ""
    property string _bSrc: ""
    property bool _useA: false

    onResolvedArtChanged: {
        if (root._useA) { root._bSrc = root.resolvedArt; root._useA = false; }
        else            { root._aSrc = root.resolvedArt; root._useA = true; }
    }

    Image {
        anchors.fill: parent
        fillMode: Image.PreserveAspectCrop
        smooth: true; mipmap: true; asynchronous: true; cache: true
        sourceSize.width: 64; sourceSize.height: 64
        source: root._aSrc
        opacity: root._useA && status === Image.Ready ? 1 : 0
        Behavior on opacity { NumberAnimation { duration: Appearance.animation.elementMoveFast.duration; easing.type: Easing.OutCubic } }
    }
    Image {
        anchors.fill: parent
        fillMode: Image.PreserveAspectCrop
        smooth: true; mipmap: true; asynchronous: true; cache: true
        sourceSize.width: 64; sourceSize.height: 64
        source: root._bSrc
        opacity: !root._useA && status === Image.Ready ? 1 : 0
        Behavior on opacity { NumberAnimation { duration: Appearance.animation.elementMoveFast.duration; easing.type: Easing.OutCubic } }
    }

    MaterialSymbol {
        anchors.centerIn: parent
        visible: !root.resolvedArt
        text: "music_note"
        fill: 1
        iconSize: Math.max(10, root.width * 0.55)
        color: Appearance.m3colors.m3onSurface
    }
}
